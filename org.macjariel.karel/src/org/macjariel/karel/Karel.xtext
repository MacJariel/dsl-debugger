grammar org.macjariel.karel.Karel with org.eclipse.xtext.common.Terminals

generate karel "http://www.macjariel.org/karel/Karel"

Program:
	'BEGIN-PROGRAM'
	(initSection=InitSection)?
	(userDefinedCommands+=UserDefinedCommand)*
	(main=Main)
	'END-PROGRAM';

InitSection:
	'BEGIN-INIT'
	(initCommands+=InitCommand)+
	'END-INIT';

InitCommand:
	InitPositionCommand | InitHeadingCommand | InitBoardSizeCommand;

InitPositionCommand:
	'position' x=INT ',' y=INT ';';

InitHeadingCommand:
	'heading' heading=HeadingKind ';';

InitBoardSizeCommand:
	'boardSize' width=INT ',' height=INT ';';

HeadingKind:
	"NORTH" | "SOUTH" | "WEST" | "EAST";

UserDefinedCommand:
	(doc=DocumentationComment)?
	'DEFINE COMMAND' name=ID 'AS'
	'BEGIN'
	(statements+=Statement)*
	'END';

Main:
	{Main}
	'BEGIN-MAIN'
	(statements+=Statement)*
	'END-MAIN';

Statement:
	IfStatement | IterateStatement | WhileStatement | BreakableStatement
;

BreakableStatement:
	CommandStatement | UserDefinedCommandStatement
;

UserDefinedCommandStatement:
	command=[UserDefinedCommand] ';';

IfStatement:
	'IF' '(' condition=ConditionExpr ')' '{'
	(statements+=Statement)*
	'}'
	('ELSE' '{'
	(elseStatements+=Statement)*
	'}')?;

IterateStatement:
	'ITERATE' times=INT 'TIMES' '{'
	statements+=Statement
	'}';

WhileStatement:
	'WHILE' '(' condition=ConditionExpr ')' '{'
	statements+=Statement
	'}';

DocumentationComment:
	'@(' text=STRING ')';

CommandStatement:
	kind=CommandKind ";";

CommandKind:
	"move" | "turn" | "turnOn" | "turnOff" | "put" | "get";

ConditionExpr:
	negation?='!' expr=ConditionKind;

ConditionKind:
	"WALL_AHEAD" | "MARKED_PLACE";